# $Id: Portfile 143513 2015-12-14 04:01:44Z mmoll@macports.org $

PortSystem 1.0
PortGroup           cmake 1.0
PortGroup           active_variants 1.1
PortGroup           compiler_blacklist_versions 1.0
cmake.out_of_source yes

name                ompl
version             1.1.0
revision            4
categories          science
maintainers         mmoll
description         The Open Motion Planning Library (OMPL)
long_description    The Open Motion Planning Library (OMPL) consists of a set \
                    of sampling-based motion planning algorithms.
homepage            http://ompl.kavrakilab.org
platforms           darwin
license             BSD
master_sites        https://bitbucket.org/ompl/ompl/downloads
distname            ${name}-${version}-Source
checksums           rmd160  eea0fe84518fc9a8e37e36c100e5a158218b5296 \
                    sha256  4d141ad3aa322c65ee7ecfa90017a44a8114955316e159b635fae5b5e7db74f8
depends_lib-append  port:boost port:ode port:flann port:triangle port:eigen3

# We "inherit" the compiler blacklist from the Boost port, since OMPL depends heavily on Boost.
compiler.blacklist {clang < 421} *llvm-gcc-4.2 *gcc-4.0 gcc-3.3

# no universal of gccxml-devel
universal_variant   no
configure.args-append   -DOMPL_BUILD_DEMOS=OFF
# generate the extra C++ code needed for Python bindings. This code seems
# somewhat dependent on compiler and/or boost version, so we can't
# pre-generate them and make them available as an extra download.
post-configure {
    # enable parallel build on at most 2 cores. Generating the bindings
    # uses large amounts of memory, so don't use more cores.
    if { ${use_parallel_build} } { set cj "-j 2" } else { set cj "" }
    system -W ${cmake.build_dir} "${build.cmd} ${cj} update_bindings"
}
configure.args-append   -DOMPL_BUILD_DEMOS=OFF

# The +app variant includes all the content of the regular OMPL version, but
# adds a simple GUI and an extra library with bindings to the assimp and fcl
# libraries. The cascade of dependencies due to pyqt4 is rather large, so by
# default the +app variant is not enabled. Also, the additions to OMPL in
# this variant are distributed under a slightly different license than the
# regular OMPL version.
variant app description {Include GUI and extra demo code} {
    license             noncommercial
    pre-extract {
        ui_warn "The +app variant causes some extra code to be compiled that is
distributed under the Rice University Software Distribution License. For
details, please read:
http://ompl.kavrakilab.org/license.html#ricelicense"
    }
    depends_build-append port:pkgconfig
    depends_lib-append  port:assimp port:fcl port:redis
    configure.args-append -DOMPLAPP_PQP=OFF -DOMPLAPP_RESOURCE_DIR=${prefix}/share/ompl/resources
    distname            omplapp-${version}-Source
    checksums           rmd160  8d42b0ce881c6ef093d135e917997079bf52a99c \
                        sha256  611022e00a53f3d7d5b44d558f243fe9121a1eb050a0036767dc8eb706f313b1
    notes "
    If you intend to use ompl_webapp, you need to first launch the redis server:

        sudo port load redis
    "
}

patchfiles patch-detect-eigen.diff
if { [variant_isset app] } {
    patchfiles-append patch-detect-eigen-omplapp.diff
} else {
    patch.pre_args      -p1
}

set pythons_suffixes {27 34 35}

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

proc python_dir {} {
    global pythons_suffixes
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set p python[string index ${s} 0].[string index ${s} 1]
            return [file normalize [exec ${p} -c "import sys; print(sys.prefix)"]/lib/${p}/site-packages]
        }
    }
    error "Python support not enabled."
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    eval [subst {
        variant ${p} description "Use Python ${v} for python bindings" conflicts ${c} debug {
            depends_build-append    port:py${s}-pyplusplus
            depends_lib-append      port:${p}
            configure.args-append   -DPYTHON_EXEC=${prefix}/bin/python${v}

            # the python bindings rely on Boost.Python, so make sure it is installed.
            require_active_variants boost ${p}

            if { [variant_isset app] } {
                # Can use either py*-pyqt4 or py*-pyqt4-devel.
                depends_lib-append \
                    path:share/py${s}-sip/PyQt4:py${s}-pyqt4 \
                    port:py${s}-opengl \
                    port:py${s}-flask \
                    port:py${s}-celery \
                    port:py${s}-redis
                post-destroot {
                    reinplace "s|#!/usr/bin/env python|#!${prefix}/bin/python${v}|g" \
                        ${destroot}${prefix}/bin/ompl_app \
                        ${destroot}${prefix}/bin/ompl_benchmark_statistics.py
                    reinplace "s|broker=rabbitmq|broker=redis|g" \
                        ${destroot}${prefix}/share/ompl/ompl.conf
                    reinplace "s|python ./omplweb.py|${prefix}/bin/python${v} ./omplweb.py|g" \
                        ${destroot}${prefix}/share/ompl/webapp/omplweb.py
                    # extra copy of libs not needed in site_packages and causes problems with rpaths
                    set site_packages \[python_dir\]
                    delete ${destroot}\${site_packages}/ompl/util/libompl.dylib \
                           ${destroot}\${site_packages}/ompl/app/libompl_app.dylib \
                           ${destroot}\${site_packages}/ompl/app/libompl_app_base.dylib
                }
            } else {
                post-destroot {
                    reinplace "s|#!/usr/bin/env python|#!${prefix}/bin/python${v}|g" \
                        ${destroot}${prefix}/bin/ompl_benchmark_statistics.py
                    # extra copy of libs not needed in site_packages and causes problems with rpaths
                    set site_packages \[python_dir\]
                    delete ${destroot}\${site_packages}/ompl/util/libompl.dylib
                }
            }
        }
    }]
}

# enable one python variant so that the python bindings can be compiled
set selected_python python27
foreach s ${pythons_suffixes} {
    if {[variant_isset python${s}]} {
        set selected_python python${s}
    }
}
default_variants +${selected_python}

livecheck.type  regex
livecheck.url   http://ompl.kavrakilab.org/download.html
livecheck.regex (\[0-9.\]+), released
