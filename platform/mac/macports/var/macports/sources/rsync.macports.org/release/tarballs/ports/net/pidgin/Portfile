# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id: Portfile 134962 2015-04-12 08:20:29Z devans@macports.org $

PortSystem          1.0

name                pidgin
version             2.10.11
revision            1
categories          net
platforms           darwin
license             GPL-2+
maintainers         devans openmaintainer
description         Multi-protocol Instant Messaging client
long_description    Pidgin is an Open Source clone of America OnLine's \
                    Instant Messenger client that uses GTK+ version 2.  It \
                    features multiple groups, group chat, user information \
                    fetching and registration, time stamping, and more.  It \
                    also supports other IM protocols, including Yahoo!, MSN, \
                    ICQ, Jabber, Napster, IRC, and Zephyr via included \
                    plugins.

homepage            http://www.pidgin.im/
master_sites        sourceforge:project/pidgin/Pidgin/${version}

checksums           rmd160  93802d5884b5ffd0714cb91dbe31f4e311f17e6d \
                    sha256  f2ae211341fc77efb9945d40e9932aa535cdf3a6c8993fe7919fca8cc1c04007

use_bzip2           yes

# Python is only required if --enable-dbus is used, and only for
# the build process to generate the dbus code, not for running pidgin.

depends_build       port:pkgconfig \
                    port:intltool \
                    port:autoconf \
                    port:automake \
                    port:libtool \
                    port:gsed \
                    port:python27

depends_lib         path:lib/pkgconfig/glib-2.0.pc:glib2 \
                    port:libxml2 \
                    port:gconf \
                    port:libidn \
                    port:gnutls \
                    port:perl5.16

if {${subport} ne "libpurple"} {
    depends_lib-append \
                    port:libpurple \
                    port:audiofile \
                    port:gtk2 \
                    port:libao

    post-destroot {
        foreach file [glob -nocomplain -directory ${destroot}${prefix}/bin purple-*] {
            delete $file
        }
        foreach file [glob -nocomplain -directory ${destroot}${prefix}/lib pkgconfig/purple.pc libpurple* purple-2] {
            delete $file
        }
        delete ${destroot}${prefix}/include/libpurple \
               ${destroot}${prefix}/share/aclocal \
               ${destroot}${prefix}/share/man/man3/Purple.3pm \
               ${destroot}${prefix}/share/purple \
               ${destroot}${prefix}/share/sounds/purple \
    }
}

patchfiles          patch-configure.ac.diff \
                    patch-libpurple-internal.h.diff

# reconfigure using upstream autogen.sh for intltool 0.51 compatibility

post-patch {
    xinstall -m 755 ${filespath}/autogen.sh ${worksrcpath}
}

configure.cmd       ./autogen.sh

# --disable-dependency-tracking was added because a +universal perl will result in
# PERL_CFLAGS containing multiple -arch arguments... this is the easiest workaround

configure.args      --disable-avahi \
                    --disable-consoleui \
                    --disable-farstream \
                    --disable-gstreamer \
                    --disable-gstreamer-interfaces \
                    --disable-gtkspell \
                    --disable-meanwhile \
                    --disable-nm \
                    --disable-screensaver \
                    --disable-sm \
                    --disable-startup-notification \
                    --disable-schemas-install \
                    --disable-vv \
                    --disable-nss \
                    --mandir=${prefix}/share/man \
                    --with-python=${prefix}/bin/python2.7 \
                    --disable-dependency-tracking \
                    --without-x

configure.args-append ac_cv_path_perlpath=${prefix}/bin/perl5.16

configure.ldflags-append    -bind_at_load
configure.cppflags-append   -no-cpp-precomp

subport libpurple {
    configure.args-append   --disable-gtkui \
                            --disable-pixmaps-install \
                            --disable-nls
    pre-activate {
        if {![catch {set installed [lindex [registry_active pidgin] 0]}]} {
            set _version [lindex $installed 1]
            if {[vercmp $_version 2.10.9] < 0} {
                registry_deactivate_composite pidgin "" [list ports_nodepcheck 1]
            }
        }
    }
}

if {${subport} eq $name} {

    variant finch_only requires finch description {Build without pidgin} {
        configure.args-append   --disable-gtkui
    }

    variant finch description {Build with finch} {
        configure.args-delete   --disable-consoleui
        patchfiles-append       patch-finch_libgnt_Makefile.in.diff patch-finch_libgnt_gntwm.c
    }

    variant x11 conflicts no_x11 quartz {
        depends_lib-append      port:xorg-libX11 \
                                port:xorg-libXext \
                                port:xorg-libXScrnSaver \
                                port:xorg-libice \
                                port:xorg-libsm

        configure.args-delete   --without-x \
                                --disable-screensaver \
                                --disable-sm

        configure.args-append   --with-x \
                                --x-includes=${prefix}/include \
                                --x-libraries=${prefix}/lib \
                                --enable-screensaver \
                                --enable-sm
    }

    # Legacy no_x11 variant to be removed after May 2015.
    variant no_x11 conflicts x11 description {Legacy compatibility variant} {}

    if {[variant_isset no_x11] || [variant_isset quartz]} {
        default_variants -x11
    } else {
        default_variants +x11
    }

    variant quartz conflicts x11 {
        configure.args-append   --disable-gestures
    }

    variant spellcheck description {Allow spell checking} {
        depends_lib-append      port:gtkspell2
        configure.args-delete   --disable-gtkspell
    }

    post-activate {
        system "dbus-uuidgen --ensure"
        system "${prefix}/bin/gtk-update-icon-cache -f -t ${prefix}/share/icons/hicolor"
        system "export GCONF_CONFIG_SOURCE=`gconftool-2 --get-default-source` && \
            gconftool-2 --makefile-install-rule ${prefix}/etc/gconf/schemas/*.schemas"
    }
}
    
post-destroot {
    fs-traverse file ${destroot}${prefix}/lib/perl5 {
        if {[file tail ${file}] == "perllocal.pod"} {
            delete ${file}
        }
    }
}

livecheck.type      regex
livecheck.url       http://sourceforge.net/projects/pidgin/files/Pidgin/
livecheck.regex     "\/Pidgin\/(\\d+(?:\\.\\d+)*)\/"
